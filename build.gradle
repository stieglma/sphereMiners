apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

mainClassName = 'me.stieglmaier.sphereMiners.main.SphereMiners'

repositories {
    ivy {
        url "http://www.sosy-lab.org/ivy"
        layout "pattern", {
            artifact "[organisation]/[module]/[artifact]-[revision].[ext]"
        }
    }
    mavenCentral()
}

task copyToLib << {
    configurations.runtime.getResolvedConfiguration().getResolvedArtifacts().each { artifact ->
        project.copy {
            from artifact.file
            into "lib"
            rename { "${artifact.name}.${artifact.extension}" }
        }
    }
}

task buildDocumentation (type:JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args("--printOptionsTo=ConfigurationOptions.txt")
}

dependencies {
    compile configuration: "runtime", group: "com.google.guava", name: "guava", version: "18.0"
    compile configuration: "runtime", group: "org.sosy_lab", name:"common", version:"0.531"
}

sourceCompatibility = 1.8
version = '0.1'

jar {
    manifest {
        attributes 'Implementation-Title': 'Sphere Miners',
                   'Implementation-Version': version,
                   'Main-Class': 'me.stieglmaier.sphereMiners.main.SphereMiners'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

uploadArchives {
    repositories {
        flatDir {
            dirs 'repos'
        }
    }
}
