apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.sherter.google-java-format'

mainClassName = 'me.stieglmaier.sphereMiners.main.SphereMiners'

repositories {
    ivy {
        url "http://www.sosy-lab.org/ivy"
        layout "pattern", {
            artifact "[organisation]/[module]/[artifact]-[revision](-[classifier]).[ext]"
        }
    }
    mavenCentral()
}

buildscript {
    repositories {
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath group: "com.github.sherter.googlejavaformatgradleplugin", name: "google-java-format-gradle-plugin", version: "0.3-SNAPSHOT"
    }
}

task getConfigOptions(type:JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args("--printOptionsTo=ConfigurationOptions.txt")
}

dependencies {
    compile configuration: "runtime", group: "com.google.guava", name: "guava", version: "18.0"
    compile configuration: "runtime", group: "org.sosy_lab", name:"common", version:"0.531"
}

sourceCompatibility = 1.8
version = '0.1'

jar {
    manifest {
        attributes 'Implementation-Title': 'Sphere Miners',
                   'Implementation-Version': version,
                   'Main-Class': 'me.stieglmaier.sphereMiners.main.SphereMiners'
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Sphere Miners',
                   'Implementation-Version': version,
                   'Main-Class': 'me.stieglmaier.sphereMiners.main.SphereMiners'
    }
    from sourceSets.main.allSource
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
